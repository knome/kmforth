
# shorthand syntax for defining enums, values, and a with-repr functions for them
# 
# @ enums/define-u64 enum-name (
#   (name      value)
#   (other     value)
#   (different value)
# );
# 
# basic helpers to get the numeric and string values of an enum entry
# enum-name//name.value
# enum-name//name.string
# 
# determine if a given value is a valid entry of the enum
# enum-name.is-valid
# 
# determine if a given or'd value is constructed only a valid enum values
# (less sure I'll actually need this one, I'll leave it for later, if ever)
# enum-name.is-valid-set
# 
# these allow the enum to be used as a type in a struct
# enum-name.size
# enum-name.get
# enum-name.set
# enum-name.with-repr

% enums/define-u8  (@typename @enums) @ enums/-define @typename type/u8  @enums ; ;
% enums/define-u16 (@typename @enums) @ enums/-define @typename type/u16 @enums ; ;
% enums/define-u32 (@typename @enums) @ enums/-define @typename type/u32 @enums ; ;
% enums/define-u64 (@typename @enums) @ enums/-define @typename type/u64 @enums ; ;

% enums/-define (@typename @basetype @enums)
  @ enums/-define-per @typename @basetype @enums ;
  @ enums/-define-struct-type-accessors @typename @basetype @enums ;
  @ enums/-define-is-valid @typename @basetype @enums ;
#   @ enums/-define-is-valid-set @typename @basetype @enums ;
;

% enums/-define-per (@typename @basetype @e.each)
  @ enums/-define-dot-value @typename @basetype @e @e ;
  @ enums/-define-dot-string @typename @e @e ;
;

% enums/-define-dot-value (@typename @basetype @en.first @ev.rest.first)
  : @typename//@en.value @ev ;
;

% enums/-define-dot-string (@typename @en.first @ens.first.string)
  : @typename//@en.string @ens ;
;

% enums/-define-struct-type-accessors (@typename @basetype @enums)
  : @typename.size @basetype.size ;
  : @typename.get  @basetype.get  ;
  : @typename.set  @basetype.set  ;
  
  : @typename.with-repr { $value $todo }
    0
    @(
      (@enums)
      (@e.each)
      
      @(
        (@e @e)
        (@skipZero.rest.first.if (@name @value))
        
        $value.get @value and
        [
          dup
          [
            "|" swap incr
          ]
          when
          "@typename@//@name@" swap incr
          
          # whenever we get a match, subtract it from the
          # remaining value so that when we get to the end,
          # we can use remaining unknown values to trigger
          # adding an unknown entry, instead of just checking
          # if anything matched, which is errant in a partial
          # matching case
          [@value -] $value.update
        ]
        when
      )
    )
    
    $value.get
    [
        # leftover values, add an unknown before calling
        
        # (also add a | if there were previous matches)
        dup
        [
          "|" swap incr
        ]
        when
        
        $value.get
        [ { $n $v }
          "@typename@//unknown[" $v.get "]" $n.get 3 + $todo.get strings/with-strings-as-string
        ]
        strings/with-unsigned-as-string
    ]
    [
        # if nothing is leftover, we check if we had any matches
        dup
        [
           # if we had matches, then whatever we've collected is correct, send that
           $todo.get strings/with-strings-as-string
        ]
        [
           # if not, we either send a zero value if any was specified by the enum creator
           # or else send an unknown value
           
           pop
           
           @(
             # create a macrolist containing any entries with a value of 0
             ( (@( (@enums) (@zeroCheck.each.rest.first.not.if) 1)))
             ( @hasZero.count )
             
             @(
               (@hasZero)
               (@hadAZero.if)
               
               # if we had a zero, we need to find it and expand it here so we can send back a proper name for the set
               @(
                 (@enums)
                 (@e.each)
                 
                 @(
                   (@e)
                   ((@name @value))
                   
                   @(
                     (@value)
                     (@.not.if)
                     
                     "@typename@//@name@" $todo.call
                   )
                 )
               )
             )
             
             @(
               (@hasZero)
               (@didntHaveAZero.not.if)
               
               # there was no zero, so instead we just pop out an unknown for it
               "@typename//unknown[0]" $todo.call
             )
           )
        ]
        if
    ]
    if
  ;
;

% enums/-define-is-valid (@typename @basetype @enums)
  # [: v
  # [: <is v valid 1||0>
  : @typename.is-valid 
    [ { $escape }
      @(
        (@enums)
        (@e.each)
        
        @(
          (@e)
          ((@name @value))
          
          dup @value ==
          [
            pop 1 $escape.call
          ]
          when
        )
      )
      pop 0
    ]
    stack/with-escape
  ;
;
