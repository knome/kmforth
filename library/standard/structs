
# we might alter this in the future so that the caller defines a macro that accepts a macro to expand
# in order to define the fields, and then calls the defining macro using the name of their field macro.
# the advantage here is that field values would then be generally available as injectable values rather
# than only existing in the expansion of a single macro, which will allow us to arbitrarily manipulate
# them and recursively expand fields.
# 
# % my/type (%m %s)
#   @ %m %s my/type ((field definitions) (go here)) ;
# ;
# 
# @ struct/define my/type ;
# 
# :main my/type//go//subfield//othersubfield.set
# 
# or something like that

#####

:type/u8.size 1             ;
:type/u8.set  memory/poke-b ;
:type/u8.get  memory/peek-b ;

# u8-addr cursor
:type/u8.repr
  { $cursor $addr } $cursor.set
  [
    dup strings/length swap $cursor.get ~cursor/write
  ]
  strings/with-unsigned-as-string
;

:type/u16.size 2             ;
:type/u16.set  memory/poke-w ;
:type/u16.get  memory/peek-w ;

:type/u32.size 4 ;
:type/u32.set  memory/poke-d ;
:type/u32.get  memory/peek-d ;

:type/u64.size 8 ;
:type/u64.set  memory/poke-q ;
:type/u64.get  memory/peek-q ;

:type/pointer.size type/u64.size ;
:type/pointer.set  type/u64.set  ;
:type/pointer.get  type/u64.get  ;

# user defines a macro of form % name (@rc @st) @ @rc @st ( (field type) (field type) (field type count) ...etc... )
# they then call @ struct/define name ; # (rc=recursive-call st=state)
# our macro then calls their macro with its internal bits ready to go
# they could just call it directly, but making them use a macro will let me create recursive structure definitions eventually
# 
% struct/define (@definition)
  @ @definition struct/-with-definition @definition ;
;

# annotate the (name type) and (name type reps) items as (@fn @ft @is-array @items)
# annotate the 2 items ones as (@fn @ft 0 1) # 1 because we use reps*size in the field-addr calculations
# then expand the .set/etc functions for 1 and the .nth.set/etc functions for otherwise
# 
% struct/-with-definition ( @name @fields )
  @ struct/-define
      @name
      (
          @(
              (@fields)
              (@f.each)
              @(
                  (@f)
                  (@fc.count)
                  @(
                      ((@fc 2) @f)
                      (@e.ifEqual (@fn @ft))
                      (@fn @ft 0 1)
                  )
                  @(
                      ((@fc 3) @f)
                      (@e.ifEqual (@fn @ft @fr))
                      (@fn @ft 1 @fr)
                  )
              )
          )
      )
  ;
;

% struct/-define (@name @fields)
  @ struct/-with-mapping-window struct/-define-field-accessors @name @fields ;
  
  # struct-level functions
  
  : @name.size 0 @( (@fields) (@f.each) @( (@f) ((@fn @ft @fa @fr)) @ft.size @fr * + ) ) ;
;

% struct/-define-field-accessors ( @name @before (@fieldName @fieldType @fieldIsArray @fieldRepetitions) @after )
  @ struct/-define-array-accessors  @name @before @fieldName @fieldType @fieldIsArray @fieldRepetitions @after ;
  @ struct/-define-scalar-accessors @name @before @fieldName @fieldType @fieldIsArray @fieldRepetitions @after ;
  
  # common accessors
  
  # [: structaddr
  # [: fieldaddr
  # 
  : @name//@fieldName.addr
    0 @( (@before) (@b.each) @( (@b) ((@fn @ft @fa @fr)) @ft.size @fr * + ) ) +
  ;
;  

% struct/-define-array-accessors (@name @before @fieldName @fieldType @fieldIsArray.expandIf @fieldRepetitions @after)
  
  # [:
  # [: <number-of-entries>
  # 
  : @name//@fieldName.entries
    @fieldRepetitions
  ;
  
  # [:
  # [: <last-index>
  : @name//@fieldName.last-index
    @fieldRepetitions 1 -
  ;
  
  # [: index structaddr
  # [: &fieldValue[ index ]
  # 
  : @name//@fieldName.nth.addr { $index }
    @name//@fieldName.addr  #[: index fieldaddr
    swap                    #[: fieldaddr index
    @fieldType.size * +     #[: &fieldaddr[ index ]
  ;
  
  # [: index structaddr
  # [: fieldValue
  # 
  : @name//@fieldName.nth.get
    @name//@fieldName.nth.addr # [: &fieldaddr[ index ]
    @fieldType.get             # [: value
  ;
  
  # [: new-value index structaddr 
  # [: 
  # 
  : @name//@fieldName.nth.set { $value }
    @name//@fieldName.nth.addr # [: new-value &fieldaddr[ index ]
    @fieldType.set
  ;
;

% struct/-define-scalar-accessors (@name @before @fieldName @fieldType @fieldIsArray.expandIfNot @fieldRepetitions @after)
  
  # [: 
  # [: size
  # 
  : @name//@fieldName.size
    @fieldType.size
  ;
  
  # [: structaddr
  # [: fieldvalue
  # 
  : @name//@fieldName.get
    @name//@fieldName.addr
    @fieldType.get
  ;
  
  # [: value structaddr
  # [: 
  # 
  : @name//@fieldName.set { $value }
    @name//@fieldName.addr #[: value fieldaddr
    @fieldType.set
  ;
  
  # [: todo structaddr
  # [:
  #
  : @name//@fieldName.update { $structaddr $todo }
    $structaddr.copy      #[: todo structaddr
    @name//@fieldName.get #[: todo value
    swap                  #[: value todo
    call                  #[: updated-value
    $structaddr.get       #[: updated-value structaddr
    @name//@fieldName.set #[: 
  ;

  # [: structaddr
  # [: 
  : @name//@fieldName.incr
    [ 1 + ] swap @name//@fieldName.update
  ;
  
  # [: structaddr
  # [: 
  : @name//@fieldName.decr { $fieldAddr }
    [ 1 - ] swap @name//@fieldName.update
  ;
  
  # [: structaddr
  #
  : @name//@fieldName.call
    @name//@fieldName.get call
  ;
;

# for
#   @ hello (some state) (1 2 3 4 5)
# expandsTo
#   @ hello (some state) ()        1 (2 3 4 5)
#   @ hello (some state) (1)       2 (3 4 5)
#   @ hello (some state) (1 2)     3 (4 5)
#   @ hello (some state) (1 2 3)   4 (5)
#   @ hello (some state) (1 2 3 4) 5 ()
# 
% struct/-with-mapping-window (@todo @state @list)
  @ struct/-with-mapping-window/ready @todo @state () @list ;
;

# % with-mapping-window/ready (hello state (a b c) (d e f))
#   @ hello state (a b c) d (e f) ;
#   @ struct/-with-mapping-window/ready hello state (a b c d) (e f)
# ;
# 
% struct/-with-mapping-window/ready (@todo @state @prev @remaining.ifNotEmpty)
  @ @todo @state @prev @( (@remaining) (@rf.first) @rf) @( (@remaining) (@rr.rest) @rr ) ;
  @ struct/-with-mapping-window/ready
    @todo
    @state
    ( @( (@prev) (@p.each) @p ) @( (@remaining) (@rf.first) @rf ) ) 
    @( (@remaining) (@rr.rest) @rr )
  ;
;
